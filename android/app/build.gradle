apply plugin: 'kotlin-kapt'
apply plugin: "com.android.application"
apply plugin: 'kotlin-android'

import com.android.build.OutputFile

/**
 * The react.gradle file registers a task for each build variant (e.g. bundleDebugJsAndAssets
 * and bundleReleaseJsAndAssets).
 * These basically call `react-native bundle` with the correct arguments during the Android build
 * cycle. By default, bundleDebugJsAndAssets is skipped, as in debug/dev mode we prefer to load the
 * bundle directly from the development server. Below you can see all the possible configurations
 * and their defaults. If you decide to add a configuration block, make sure to add it before the
 * `apply from: "../../node_modules/react-native/react.gradle"` line.
 *
 * project.ext.react = [
 *   // the name of the generated asset file containing your JS bundle
 *   bundleAssetName: "index.android.bundle",
 *
 *   // the entry file for bundle generation
 *   entryFile: "index.android.js",
 *
 *   // whether to bundle JS and assets in debug mode
 *   bundleInDebug: false,
 *
 *   // whether to bundle JS and assets in release mode
 *   bundleInRelease: true,
 *
 *   // whether to bundle JS and assets in another build variant (if configured).
 *   // See http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Build-Variants
 *   // The configuration property can be in the following formats
 *   //         'bundleIn${productFlavor}${buildType}'
 *   //         'bundleIn${buildType}'
 *   // bundleInFreeDebug: true,
 *   // bundleInPaidRelease: true,
 *   // bundleInBeta: true,
 *
 *   // whether to disable dev mode in custom build variants (by default only disabled in release)
 *   // for example: to disable dev mode in the staging build type (if configured)
 *   devDisabledInStaging: true,
 *   // The configuration property can be in the following formats
 *   //         'devDisabledIn${productFlavor}${buildType}'
 *   //         'devDisabledIn${buildType}'
 *
 *   // the root of your project, i.e. where "package.json" lives
 *   root: "../../",
 *
 *   // where to put the JS bundle asset in debug mode
 *   jsBundleDirDebug: "$buildDir/intermediates/assets/debug",
 *
 *   // where to put the JS bundle asset in release mode
 *   jsBundleDirRelease: "$buildDir/intermediates/assets/release",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in debug mode
 *   resourcesDirDebug: "$buildDir/intermediates/res/merged/debug",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in release mode
 *   resourcesDirRelease: "$buildDir/intermediates/res/merged/release",
 *
 *   // by default the gradle tasks are skipped if none of the JS files or assets change; this means
 *   // that we don't look at files in android/ or ios/ to determine whether the tasks are up to
 *   // date; if you have any other folders that you want to ignore for performance reasons (gradle
 *   // indexes the entire tree), add them here. Alternatively, if you have JS files in android/
 *   // for example, you might want to remove it from here.
 *   inputExcludes: ["android/**", "ios/**"],
 *
 *   // override which node gets called and with what additional arguments
 *   nodeExecutableAndArgs: ["node"],
 *
 *   // supply additional arguments to the packager
 *   extraPackagerArgs: []
 * ]
 */

project.ext.react = [
        entryFile      : "index.js",
        bundleAssetName: "main.jsbundle",
        inputExcludes  : ["android/**", "ios/**", "node_modules/**", "packages/openland-mac/node_modules/**"],
        // bundleInRelease: false
]

apply from: "../../node_modules/react-native/react.gradle"
apply from: "../../node_modules/react-native-code-push/android/codepush.gradle"

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = false

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false

/**
 * Hack for AppCenter
 */
if ("$System.env.CI" == "true") {
    buildDir = "./app/build/"
}

android {
    dexOptions {
        preDexLibraries = false
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.openland.app"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 41
        versionName "1.0.37"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true
        // multiDexEnabled true
    }
    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk true  // If true, also generate a universal APK
            include "armeabi-v7a", "x86"
        }
    }
    buildTypes {
        release {
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a": 1, "x86": 2]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }
    productFlavors {
    }
}

configurations.all {
    resolutionStrategy {
        force 'org.webkit:android-jsc:r224109'
    }
    exclude group: 'com.facebook.infer.annotation', module: 'infer-annotation'
    exclude group: 'com.facebook.yoga', module: 'yoga'
    // exclude group: 'com.facebook.litho', module: 'litho-annotations'
    // resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.beust:klaxon:3.0.1'
    compile project(':react-native-share')
    compile project(':react-native-image-capinsets')
    implementation 'com.google.firebase:firebase-core:16.0.1'
    compile project(':react-native-haptic-feedback')
    compile project(':react-native-gesture-handler')
    compile project(':lottie-react-native')
    compile project(':react-native-linear-gradient')
    compile project(':react-native-blur')
    compile project(':react-native-view-overflow')
    compile project(':react-native-image-crop-picker')
    compile project(':react-native-fast-image')
    compile project(':rn-fetch-blob')
    compile project(':react-native-image-picker')
    compile project(':react-native-extra-dimensions-android')
    compile project(':react-native-code-push')
    compile project(':react-native-device-info')
    compile project(':react-native-push-notification')
    compile project(':react-native-vector-icons')
    compile project(':react-native-auth0')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile 'com.facebook.react:react-native:+'
    // From node_modules
    def appCenterSdkVersion = '1.7.0'
    compile "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    compile "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    compile "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    implementation 'com.facebook.litho:litho-core:0.18.0'
    implementation 'com.facebook.litho:litho-widget:0.18.0'
    implementation 'com.facebook.litho:litho-fresco:0.18.0'
    compileOnly 'com.facebook.litho:litho-annotations:0.18.0'
    kapt 'com.facebook.litho:litho-processor:0.18.0'
    annotationProcessor 'com.facebook.litho:litho-processor:0.18.0'

    implementation 'com.facebook.litho:litho-sections-core:0.18.0'
    implementation 'com.facebook.litho:litho-sections-widget:0.18.0'
    compileOnly 'com.facebook.litho:litho-sections-annotations:0.18.0'
    kapt 'com.facebook.litho:litho-sections-processor:0.18.0'
    annotationProcessor 'com.facebook.litho:litho-sections-processor:0.18.0'
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

//task bundleReleaseJsAndAssets(overwrite: true) {
//
//}

apply plugin: 'com.google.gms.google-services'